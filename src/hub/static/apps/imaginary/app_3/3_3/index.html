<!DOCTYPE html>
<html lang="en">
 <head>
    <meta http-equiv="content-type" content="text/html; charset=UTF-8">
    <meta charset="utf-8">
    <title>App 3-3</title>
    <script type="text/javascript" src="https://cindyjs.org/dist/v0.8/Cindy.js"></script>
    <script type="text/javascript" src="https://cindyjs.org/dist/v0.8/CindyGL.js"></script>

    <!--<script type="text/javascript" src="Cindy_master.js"></script>-->
</head>

<body style="font-family:Palatino; margin:0; font-size:16pt">




<script id='csinit' type='text/x-cindyscript'>


// **********************************************************************************************************
// Functions ************************************************************************************************
// **********************************************************************************************************



drawDragon() := (
  drawFlipBookObject(dragon);
  //fillcircle(dragon.pos, dragon.rad, color->(1,0.5,0));
);

drawWand() := (
  //draw([wand.pos, wand.pos + wand.value * wand.dir], color->(0.5,0,1), size-> 35, alpha->0.2);
  //drawimage(wand.pos, "wand", rotation -> -0.5*pi+arctan2(wand.dir), scale->0.3, flipx->true);
  drawimage(wand.pos, "wand", rotation -> -0.5*pi, scale->0.3, flipx->true);

  forall(1..ceil(lerp(0, magicLength, wand.value, 0, wand.maxValue)),
    drawimage((origin.x, lerp(origin.y, wand.pos.y, #, 0, magicLength)), "magic" + magicField_#_1, rotation -> magicField_#_2 + 0.5*pi-arctan2(wand.dir), scale->lerp(0.2, 0.06, #, 0, magicLength), alpha->1);
  );

  //draw([wand.pos, wand.pos + wand.length * wand.dir], color->(0,0,1), size-> 5, arrow->true, arrowsize->0.5);
  //fillcircle(wand.pos, 0.3, color->(0,0,1));
);

drawRecallButton() := (
  fillcircle(recallButton.pos, recallButton.rad, color->(0.7,0,1), alpha->0.5);
  drawcircle(recallButton.pos, recallButton.rad, color->(1,1,1), size-> 5);
  drawtext(recallButton.pos + (0,-0.4), recallButton.label, color->(1,1,1), size->30, align->"mid");
);


drawPickUp() := (
  //fillcircle(pickUp.pos, pickUp.rad, color->(0,1,0));
  drawFlipBookObject(pickUp);
);

drawScoreLabel() := (
  drawtext(scoreLabel.pos, scoreLabel.text, size->30);
);

drawObstacles() := (
  forall(obstacles,
    fillcircle(#.pos, #.rad, color->(1,0,0));
  );
);


drawDragonTrail() := (
  connect(dragon.trailB ++ apply(dragon.trailA, #_1), color->(1,0.8,0), size-> 3, alpha->0.5);
  forall(1..length(dragon.trailA) - 1,
    //fillcircle(dragon.trailA_#, lerp(0, dragon.trailAMaxSize, #, 0, length(dragon.trailA) - 1), color->(1,1,0), alpha->lerp(0, 0.5, #, 0, length(dragon.trailA) - 1));
    //drawcircle(dragon.trailA_#, lerp(0, dragon.trailAMaxSize, #, 0, length(dragon.trailA) - 1), color->(1,0.5,0));
    drawimage(dragon.trailA_#_1, "smoke" + dragon.trailA_#_2, angle->dragon.trailA_#_3, scale->0.15 * lerp(0.5, dragon.trailAMaxSize, #, 0, length(dragon.trailA) - 1), alpha->lerp(0, 1, #, 0, length(dragon.trailA) - 1));
  );
);

drawFlamma() := (
  drawFlipBookObject(flamma);
);

drawSpeechBubble() := (
  if(showSpeechBubble,
    drawimage(speechBubble.pos, speechBubble.sprite, alpha->speechBubble.alpha, flipx->true);
    drawtext(speechBubble.pos, flamma.feedback_(clamp(scoreLabel.score, 1, length(flamma.feedback))), size-> 30, align->"mid", alpha->speechBubble.alpha);
  );
);



drawClouds() := (
  forall(clouds,
    drawimage(#.pos, #.sprite, flipx->#.flipx, flipy->#.flipy, scale->#.scale, scale->[0.7,1.5]);
  );
);

















collideWithPickUp() := (
  if(dist(dragon.pos, pickUp.pos) < dragon.rad + pickUp.rad,
    scoreLabel.score = scoreLabel.score + 1;
    scoreLabel.text = scoreLabel.score;
    lastPickUpPos = pickUp.pos;
    pickUp.pos = newPickUpSpawnPoint(obstacles ++ [wand, dragon], 10);

    if(!flammaIn,
      flammaOut = false;
      flammaInTrack = setupAnimationTrack(now(), now() + 0.5);
      flammaIn = true;
      flammaInStart = flamma.pos;
    );
  );
);




collideWithObstacles() := (
  forall(obstacles,
    if(dist(dragon.pos, #.pos) < dragon.rad + #.rad,
    recallTarget = dragonHome;
      recallDragon();
    );
  );
);








moveDragon(dt) := (
  dragon.pos = dragon.pos + dragon.velocity * dt;

  if(dist(dragon.pos, (dragon.trailA)_(-1)_1) > 0.5,
    dragon.trailA = dragon.trailA :> [dragon.pos, ceil(3 * random()), 2 * pi * random()];
    if(length(dragon.trailA) > dragon.trailAMaxLength,
      dragon.trailB = dragon.trailB :> dragon.trailA_1_1;
      dragon.trailA = bite(dragon.trailA);
      );
    if(length(dragon.trailB) > dragon.trailBMaxLength,
      dragon.trailB = bite(dragon.trailB);
    );
  );
);


updateDragon() := (
  regional(speed);

  dragon.velocity = wand.dir * lerp(0, dragon.maxSpeed, wand.value, 0, wand.maxValue);
  speed = abs(dragon.velocity);
  if(speed > dragon.maxSpeed, dragon.velocity = dragon.velocity / speed * dragon.maxSpeed);
  dragon.rotation = 0.5*pi-arctan2(wand.dir);
  if(wand.dir.x < -0.1,
    dragon.rotation = -dragon.rotation;
    dragon.flipx = true;
  );
  if(wand.dir.x > 0.1,
    dragon.flipx = false;
  );
);



recallDragon() := (
  wandActive = false;
  recallStart = dragon.pos;
  recallTrack = setupAnimationTrack(now(), now() + 1);
  recallActive = true;
  wand.value = 0;
  dragon.velocity = [0,0];
  dragon.trailA = [];
  dragon.trailB = [];
);



updateWand() := (
  regional(relMouse, dist);

  relMouse = (3, mouse().y - origin.y);

  dist = abs(relMouse);
  wand.value = lerp(wand.value, clamp(dist, 0, wand.maxValue), wand.acceleration);
  if(wand.value < wand.deadzone,
    wand.value = 0;
  );
  wand.dir = relMouse; //slerp(wand.dir, relMouse / if(dist <= 0.01, 1, dist), wand.acceleration);
  wand.pos_2 = mouse().y;
);


















newPickUpSpawnPoint(list, minDist) := (
  regional(candidateValid, candidate, counter);

      candidateValid = true;

      candidate = canvasCorners_4 + [random() * canvasWidth, random() * canvasHeight];
      if(list == [],
        candidate;
      , // else //
        if(pointInPolygon(candidate, pickUpSpawnArea),
          forall(list,
            if(dist(#.pos, candidate) < minDist,
              candidateValid = false;
            );
          );
        , // else //
          candidateValid = false;
        );
        if(candidateValid,
          candidate;
        , // else //
          newPickUpSpawnPoint(list, minDist);
        );
      );
);


newObstaclePoint(list, minDist, count) := (
  regional(candidateValid, candidate, counter);

  if(count == 0,
    [];
  , // else //
    candidateValid = true;

    candidate = canvasCorners_4 + [random() * canvasWidth, random() * canvasHeight];
    if(list == [],
      candidate;
    , // else //
      forall(list,
        if(dist(#.pos, candidate) < minDist,
          candidateValid = false;
        );
      );
      if(candidateValid,
        candidate;
      , // else //
        newObstaclePoint(list, minDist, count - 1);
      );
    );
  );
);









collideWithMountains() := (
  forall(mountainPolygons :> floorPolygon,
    if(pointInPolygon(dragon.pos, #),
      dragonMoving = false;
      recallDragon();
    );
  );
);






processFlamma() := (
  if(flammaIn,
    updateAnimationTrack(flammaInTrack, delta);
    tween(flamma, "pos", flammaInStart, flamma.endPos, flammaInTrack, "easeInOutCubic");
    showSpeechBubble = (flamma.feedback_(clamp(scoreLabel.score, 1, length(flamma.feedback))) != "");
    if(showSpeechBubble,
      if(flammaInTrack.timeLeft <= 0.3,
        speechBubble.alpha = lerp(0, 1, flammaInTrack.timeLeft, 0.3, 0);
      );
    );
    if(flammaInTrack.timeLeft <= 0,
      flammaOutTrack = setupAnimationTrack(now() + 1, now() + 2);
      flammaIn = false;
      flammaOut = true;
    );
  );
  if(flammaOut,
    updateAnimationTrack(flammaOutTrack, delta);
    tween(flamma, "pos", flamma.endPos, flamma.startPos, flammaOutTrack, "easeInCirc");
    if(flammaOutTrack.timeLeft > 0.7,
      speechBubble.alpha = lerp(1, 0, flammaOutTrack.timeLeft, 1, 0.7);
    );
    if(flammaOutTrack.timeLeft <= 0,
      recallDragon();
      flammaOut = false;
      showSpeechBubble = false;

      scatterClouds();
    );
  );
);





collideWithCastle() := (
  if(pointInPolygon(dragon.pos, castlePolygon),
    if(!flammaIn,
      wandActive = false;
      flammaOut = false;
      flammaInTrack = setupAnimationTrack(now(), now() + 0.5);
      flammaIn = true;
      flammaInStart = flamma.pos;
    );
  );
);













scatterClouds() := (
  regional(spawns, gaps, newMountain);

  level = min(level + 1, 3);

  gaps = randchoose(3..7, level);

  clouds = flatten(apply(1..level, i,
    spawns = (3..10) -- (3..gaps_i);


    apply(spawns, j,
    {
      "pos": (cloudSpawnXs_i, 4 * j - 7),
      "scale": 1,
      "sprite": "cloud" + (randomint(5) + 1),
      "flipx": randombool(),
      "flipy": randombool()
    }
    );

  ));


  obstacles = apply(clouds, {"pos": #.pos, "rad": 1}) ++ apply(clouds, {"pos": #.pos + (0, 2), "rad": 1});

  mountainPolygons = [];
  mountainSprites = [];

    forall(1..level, i,
      newMountain = mountainTemplate;
      newMountain = apply(newMountain, # + (cloudSpawnXs_i, 0));
      newMountain_1_2 = 4 * (gaps_i - 1) - 8;
      mountainPolygons = mountainPolygons :> newMountain;

      mountainSprites = mountainSprites :> ["mountain" + (randomint(2) + 1), gaps_i];
    );


);


drawMountains() := (
  forall(1..level,
    drawimage([cloudSpawnXs_# - 1, 4 * (mountainSprites_#_2 - 1) - 25 - if(mountainSprites_#_1_(-1) == "1", 1, 0)], mountainSprites_#_1);
  );
);





drawAxes() := (
  draw(apply([0.02, 0.98], (lerp(canvasLeft, canvasRight, #), origin.y)), color->(1,1,1), size->10, arrow->true, arrowsize->4);
  draw(apply([0.02, 0.98], (origin.x, lerp(canvasBottom, canvasTop, #))), color->(1,1,1), size->10, arrow->true, arrowsize->4);
);








lissajoue(a, b, d, t) := [sin(a * t + d), cos(b * t)];


// **********************************************************************************************************
// Start ****************************************************************************************************
// **********************************************************************************************************
canvasCorners = apply(screenbounds(), #.xy); //LO, RO, RU, LU
canvasCenter  = 0.5 * canvasCorners_1 + 0.5 * canvasCorners_3;
canvasWidth   = dist(canvasCorners_1, canvasCorners_2);
canvasHeight  = dist(canvasCorners_1, canvasCorners_4);
[canvasLeft, canvasTop] = canvasCorners_1;
[canvasRight, canvasBottom] = canvasCorners_3;




origin = canvasCenter + (-35, 0);






dragonHome = origin;
dragon = {
  "pos": dragonHome,
  "rad": 0.6,
  "velocity": [0,0],
  "maxSpeed": 10,
  "trailA": [[dragonHome, ceil(3 * random()), 2 * pi * random()]],
  "trailAMaxSize": 1,
  "trailAMaxLength": 100,
  "trailB": [],
  "trailBMaxLength": 100,
  "flipbook": ["dragon1", "dragon2", "dragon3", "dragon2"],
  "scale": 0.3,
  "rotation": 90Â°,
  "flipx": false,
  "flipy": false,
	"index": 1
};

dragonAnimationTrack = setupAnimationTrack(0,0.6);
dragonAnimationTrack.looping = true;

wand = {
  "pos": origin + (2,0), //canvasCenter + (0,-12),
  "length": 3,
  "dir": [1,0],
  "value": 0,
  "catchRadius": 15,
  "maxValue": 10,
  "acceleration": 0.5,
  "deadzone": 1
};

magicLength = 30;
magicField = apply(1..magicLength, [randomint(3) + 1, random(2*pi)]);







mountainTemplate = [[0, 10], [-4, -10], [4, -10]];

mountainPolygons = [];
mountainSprites = [];



cloudSpawnXs = [10, 29, 44];



level = 0;
scatterClouds();


pickUpSpawnArea = [
    canvasCenter + [-35.47, 18.87],
    canvasCenter + [35.41, 19.11],
    canvasCenter + [36.01, -14.01],
    canvasCenter + [-3.47, -14.09],
    canvasCenter + [-36.27, -4.49]
];

/*
pickUp = {
  "pos": canvasCenter + [30, -5],  //newPickUpSpawnPoint(obstacles ++ [wand, dragon], 10),
  "rad": 2,
  "flipbook": ["fireball1", "fireball2", "fireball3"],
  "scale": 0.6,
  "rotation": 0,
  "flipx": false,
  "flipy": false,
	"index": 1
};
lastPickUpPos = dragonHome;

fireballAnimationTrack = setupAnimationTrack(0,0.5);
fireballAnimationTrack.looping = true;
*/

scoreLabel = {
  "pos": canvasCorners_1 + (2, -2),
  "score": 0,
  "text": "0"
};

wandActive := false;


flamma = {
  "startPos": canvasCenter + (50, -20),
  "pos": canvasCenter + (50 -20),
  "endPos": canvasCenter + (35, -15),
  "flipbook": ["flamma"],
  "scale": 0.5,
  "rotation": 0,
  "flipx": true,
  "flipy": false,
	"index": 1,
  "feedback": [
      "", "", "", "", "",
      "", "", "", "", "",
      "", "", "", "", "",
      ""
  ]
};
flammaIn = false;
flammaOut = false;


speechBubble = {
  "sprite": "bubble",
  "text": "",
  "pos": canvasCenter + (32, -5),
  "alpha": 0
};
showSpeechBubble = false;





floorPolygon = [canvasCorners_4 + (-20, 0), canvasCorners_4 + (-20, 2), canvasCorners_3 + (20, 2), canvasCorners_3 + (20, 0)];


recallButton = {
  "pos": wand.pos + (-12,-5),
  "rad": 3,
  "label": ""
};

recallActive = false;
recallStart = dragonHome;
recallTarget = dragonHome;





castlePolygon = [canvasCorners_2 + (0, 100), canvasCorners_3 + (0, -100), canvasCorners_3 + (-7, -100), canvasCorners_2 + (-7, 100)];




/*
followCurve = createRootStrokeObject(canvasCenter, (0,0,0), (1,1,1), 0);
curve(t) := 10 * lissajoue(1, 2, pi/3, t);
followCurve.stroke = samplecurve("curve", 0, 2*pi);
followCurve.drawPercent = 1;
*/


playanimation();
setupTime();
</script>
<script src="../../Egdod/egdodUI.js"></script>
<script src="../../Egdod/egdodMath.js"></script>
<script src="../../Egdod/egdodArray.js"></script>
<script src="../../Egdod/egdodColor.js"></script>
<script src="../../Egdod/egdodMisc.js"></script>
<script src="../../Egdod/egdodAnim.js"></script>




<script id='csdraw' type='text/x-cindyscript'>


  drawimage(canvasCorners_4, canvasCorners_3, "back");
  drawimage(canvasCorners_4, canvasCorners_3, "floor");
  drawimage(canvasCenter + (40, -7), "castle", scale->0.75);

  drawAxes();

  drawDragonTrail();
  //drawPickUp();
  drawDragon();
  drawClouds();
  //drawObstacles();
  //drawimage(canvasCorners_4, canvasCorners_3, "front");
  drawFlamma();
  drawWand();

  //drawStrokeObject(followCurve);

  drawSpeechBubble();


  drawRecallButton();
  //drawScoreLabel();

  drawMountains();
</script>


<script id='cstick' type='text/x-cindyscript'>
  delta = deltaTime();

  updateAnimationTrack(dragonAnimationTrack, delta * lerp(1, 2, wand.value, 0, wand.maxValue));
  //updateAnimationTrack(fireballAnimationTrack, delta);
  animateFlipbook(dragon, dragonAnimationTrack);
  //animateFlipbook(pickUp, fireballAnimationTrack);

  if(recallActive,
    updateAnimationTrack(recallTrack, delta);
    tween(dragon, "pos", recallStart, recallTarget, recallTrack, "easeInOutCubic");
    if(recallTrack.timeLeft <= 0,
      recallActive = false;
      dragon.trailA = [[dragon.pos, ceil(3 * random()), 2 * pi * random()]];
    );
  , // else //
    if(wandActive,
      moveDragon(delta);
    );
    //collideWithPickUp();
    collideWithObstacles();
    collideWithMountains();
    if(wandActive,
      collideWithCastle();
    );
  );



  processFlamma();
</script>



<script id='csmousedown' type='text/x-cindyscript'>
  if(!recallActive,
    wandActive = true;
    updateWand();
    updateDragon();
    repaint();
  );

  if(!recallActive & dist(mouse().xy, recallButton.pos) <= recallButton.rad,
    recallTarget = dragonHome;
    recallDragon();
  );



</script>

<script id='csmousedrag' type='text/x-cindyscript'>
  if(wandActive,
    updateWand();
    updateDragon();
  );
</script>

<script id='csmousemove' type='text/x-cindyscript'>
</script>

<script id='csmouseup' type='text/x-cindyscript'>
  if(wandActive,
    updateWand();
    updateDragon();
  );

  wandActive = false;
  wand.value = 0;
</script>

<div align="center">
<canvas id="CSCanvas" valign="top" width="1920" height="1080"></canvas>
</div>

<script>

    cindy = createCindy({canvasname:"CSCanvas",
                 scripts:"cs*",
                 images:{
                   smoke1: "../../assets/smoke_4.png",
                   smoke2: "../../assets/smoke_5.png",
                   smoke3: "../../assets/smoke_3.png",
                   dragon1: "../../assets/bird_1.png",
                   dragon2: "../../assets/bird_2.png",
                   dragon3: "../../assets/bird_3.png",
                   magic1: "../../assets/magic_1.png",
                   magic2: "../../assets/magic_2.png",
                   magic3: "../../assets/magic_3.png",
                   fireball1: "../../assets/fireball_1.png",
                   fireball2: "../../assets/fireball_2.png",
                   fireball3: "../../assets/fireball_3.png",
                   cloud1: "../../assets/cloud_1.png",
                   cloud2: "../../assets/cloud_2.png",
                   cloud3: "../../assets/cloud_3.png",
                   cloud4: "../../assets/cloud_4.png",
                   cloud5: "../../assets/cloud_5.png",
                   back:   "../../assets/back_3.png",
                   front:  "../../assets/front_2.png",
                   wand:   "../../assets/hand_1.png",
                   flamma:   "../../assets/flamma_wave.png",
                   bubble:   "../../assets/speech_bubble.png",
                   castle:   "../../assets/castle.png",
                   floor:   "../../assets/floor_2.png",
                   mountain1:  "../../assets/mountain_1.png",
                   mountain2: "../../assets/mountain_2.png",
                 }
                });

</script>

<script src=".././translation.js"></script>
<script>
  function getParameterByName(name, url = window.location.href) {
    name = name.replace(/[\[\]]/g, '\\$&');
    var regex = new RegExp('[?&]' + name + '(=([^&#]*)|&|#|$)'),
        results = regex.exec(url);
    if (!results) return null;
    if (!results[2]) return '';
    return decodeURIComponent(results[2].replace(/\+/g, ' '));
  }



  function translate() {
    lang = getParameterByName("lang");



    cindy.evokeCS('flamma.feedback = [];');
    for(bark of translation[lang].flamma) {
      cindy.evokeCS('flamma.feedback = flamma.feedback :> "' + bark + '";');
    }

  }

</script>



</body>

</html>
